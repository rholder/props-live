allprojects {
    apply plugin: 'idea'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'signing'

    group = 'com.github.dirkraft'

    configurations {
        // use commons-lang3 only
        all*.exclude group: 'commons-lang', module:'commons-lang', version:'2.6'
    }

    repositories {
        mavenCentral()
    }

    dependencies {

        compile 'ch.qos.logback:logback-classic:1.0.7'

        // test components
        testCompile 'junit:junit:4.10'
        testCompile 'org.mockito:mockito-core:1.9.0'
    }

    jar {
        manifest {
            def git_cmd = 'git rev-parse HEAD'
            def git_proc = git_cmd.execute()
            attributes 'SCM-Revision': git_proc.text.trim()
            attributes 'Timestamp': String.valueOf(System.currentTimeMillis())
            attributes 'Build-Host': InetAddress.localHost.hostName
        }
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from 'build/docs/javadoc'
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        classifier = 'sources'
        manifest {
            def git_cmd = 'git rev-parse HEAD'
            def git_proc = git_cmd.execute()
            attributes 'SCM-Revision': git_proc.text.trim()
            attributes 'Timestamp': String.valueOf(System.currentTimeMillis())
            attributes 'Build-Host': InetAddress.localHost.hostName
        }
    }

    artifacts {
        archives jar
        archives javadocJar
        archives sourcesJar
    }

    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories.mavenDeployer {
            // only necessary for staging/release
//            beforeDeployment { MavenDeployment deployment -> signPom(deployment) }

            print project.version
            repository(
                    // for snapshots
                    url: "https://oss.sonatype.org/content/repositories/snapshots/"
                    // for staging/release
//                    url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            ) {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
        }
    }
}

idea.project.languageLevel = 1.7
